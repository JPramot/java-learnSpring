plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'dev.lpa'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

configurations {
    agent
}

dependencies {
	implementation("io.opentelemetry:opentelemetry-api")
    agent("io.opentelemetry.javaagent:opentelemetry-javaagent:2.8.0")

	//* JDBC
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	//* Flyway migration
  	implementation 'org.flywaydb:flyway-core'
  	implementation 'org.flywaydb:flyway-database-postgresql'
	//* actuator log
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	//* validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	
	implementation 'org.springframework.boot:spring-boot-starter-web'
	//* security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	//* postgres
	runtimeOnly 'org.postgresql:postgresql'
	//* prometheus
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	//* test
  	testImplementation 'org.springframework.boot:spring-boot-starter-test'
  	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  	testImplementation 'org.testcontainers:junit-jupiter'
  	testImplementation 'org.testcontainers:postgresql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

task copyAgent(type: Copy) {
    from configurations.agent {
        rename "opentelemetry-javaagent-.*\\.jar", "opentelemetry-javaagent.jar"
    }
    into layout.buildDirectory.dir("agent")
}

tasks.named('bootJar') {
    dependsOn(copyAgent)
    archiveFileName = "app.jar"
}